package cep;

import com.ftn.sbnz.model.patient.Patient;
import com.ftn.sbnz.model.procedure.Procedure;
import com.ftn.sbnz.model.events.HeartBeatEvent;
import com.ftn.sbnz.model.events.SAPEvent;
import com.ftn.sbnz.model.events.BreathEvent;
import com.ftn.sbnz.model.events.PulseOximetryEvent;
import com.ftn.sbnz.model.events.ExtrasystoleEvent;
import com.ftn.sbnz.model.dto.RetardDTO;
import com.ftn.sbnz.model.procedure.Alarm;


import com.ftn.sbnz.service.SocketService;

import com.ftn.sbnz.model.events.SymptomEvent;

global SocketService socketService;

declare VasopressorIV
    @role(event)
    @expires(16m)
    patientId: Long
end

query "getAlarms"
    $alarm : Alarm()
end

rule "update bpm"
    when
        $patient : Patient($id: id)
        $procedure: Procedure(patientId == $id, intraOperative != null, $intraOp: intraOperative)
        Number($count : intValue) from accumulate(
            $heartBeat: HeartBeatEvent(patientId == $id)
            over window: time(1m), count($heartBeat)
        )
    then
        modify($intraOp){
            setBpm($count)
        }
        socketService.sendBpm($count, $procedure.getId());
        System.out.println("BPM::" + $intraOp);
end

rule "update sap"
    when
        $patient : Patient($id: id)
        $procedure: Procedure(patientId == $id, intraOperative != null, $intraOp: intraOperative)
        Number($count : doubleValue) from accumulate(
            $sap: SAPEvent(patientId == $id) over window: time(5m), average($sap.getValue())
        )
    then
        modify($intraOp){
            setSap($count)
        }
        socketService.sendSap($intraOp.getSap(), $procedure.getId());
        System.out.println("SAP::" + $intraOp);
end


rule "exstrasystole rule"
    when
        $patient : Patient($id: id)
        $procedure: Procedure(patientId == $id, $procedureId: id, $doctorId: medicalStaffId, $intraOp: intraOperative, postOperative == null)
        Number($count : intValue) from accumulate(
            $exs: ExtrasystoleEvent(patientId == $id)
            over window: time(5m), count($exs)
        )
        eval($count >= 25)
    then
        modify($intraOp){
            setExtrasystoleCounter($count)
        }
        System.err.println("EXTRA::" + $intraOp);
        //предиктор срчаних компликација, алармирај лекара
        System.out.println("Patient " + $patient.getFullname() + ": Detected more than 25 exstrasystoles in 5 minutes. Cardiovascular complications are possible.");
        insert(new Alarm($id, $doctorId, "Detected more than 25 exstrasystoles in 5 minutes. Cardiovascular complications are possible."));
end

rule "tachycardia rule"
    when
        $patient: Patient($id: id)
        $procedure: Procedure(patientId == $id, $procedureId: id)
        Number($count : intValue) from accumulate(
            $heartBeat: HeartBeatEvent(patientId == $id)
            over window: time(1m), count($heartBeat)
        )
        eval($count > 80)
    then
        //tahikardija
        insert(new SymptomEvent($id, $procedureId, SymptomEvent.Symptom.Tachycardia));
end

rule "tachycardia alarm"
    when 
        $patient: Patient($id: id)
        $procedure: Procedure(patientId == $id, $doctorId: medicalStaffId, $procedureId: id)
        exists(SymptomEvent(patientId == $id, procedureId == $procedureId, symptom == SymptomEvent.Symptom.Tachycardia))
    then
        System.out.println("Patient " + $patient.getFullname() + ": Detected tachycardia.");
        insert(new Alarm($id, $doctorId, "Detected tachycardia."));
end

rule "bradycardia rule"
    when
        $patient: Patient($id: id)
        $procedure: Procedure(patientId == $id, $procedureId: id, $start: start)
        // $retard: RetardDTO(procedureId == $procedureId, $current: unixTime)
        Number($count : intValue) from accumulate(
            $heartBeat: HeartBeatEvent(patientId == $id)
            over window: time(1m), count($heartBeat)
        )
        eval($count < 60 && $count > 0)
        // eval($start + 60000 < $current)
    then
        //bradikardija
        System.out.println("Bradycardia detected.");
        insert(new SymptomEvent($id, $procedureId, SymptomEvent.Symptom.Bradycardia));    
end

rule "bradycardia alarm"
    when 
        $patient: Patient($id: id)
        $procedure: Procedure(patientId == $id, $doctorId: medicalStaffId, $procedureId: id)
        exists(SymptomEvent(patientId == $id, procedureId == $procedureId, symptom == SymptomEvent.Symptom.Bradycardia))
    then
        System.out.println("Patient " + $patient.getFullname() + ": Detected bradycardia.");
        insert(new Alarm($id, $doctorId, "Detected bradycardia."));
end

rule "hypertension rule"
    when
        $patient: Patient($id: id, $basalSAP: basalSAP)
        $procedure: Procedure(patientId == $id, $procedureId: id, postOperative == null)
        Number($count : doubleValue) from accumulate(
            $sap: SAPEvent(patientId == $id) over window: time(5m), average($sap.getValue())
        )
        eval($count > 130 || $count > ($basalSAP * 1.2))
    then
        //hipertenzija
        insert(new SymptomEvent($id, $procedureId, SymptomEvent.Symptom.Hypertension));
end

rule "hypertension alarm rule"
    when
        $patient: Patient($id: id)
        $procedure: Procedure(patientId == $id, $doctorId: medicalStaffId, $procedureId: id ,postOperative == null)
        exists(SymptomEvent(patientId == $id, procedureId == $procedureId, symptom == SymptomEvent.Symptom.Hypertension))
    then
        System.out.println("Patient " + $patient.getFullname() + ": Detected hypertension.");
        insert(new Alarm($id, $doctorId, "Detected hypertension."));
end

rule "hypotension rule"
    when
        $patient: Patient($id: id)
        $procedure: Procedure(patientId == $id, $procedureId: id, postOperative == null)
        Number($averageVal : doubleValue) from accumulate(
            $sap: SAPEvent(patientId == $id)
            over window: time(5m), average($sap.getValue())
        )
        eval(($averageVal < 80 && $averageVal > 0) || ($averageVal < ($patient.getBasalSAP() * 0.8)))
    then
        System.out.println("Hypotension detected.");
        insert(new SymptomEvent($id, $procedureId, SymptomEvent.Symptom.Hypotension));
end

rule "hypotension alarm rule"
    when
        $patient: Patient($id: id)
        $procedure: Procedure(patientId == $id, $doctorId: medicalStaffId, $procedureId: id, postOperative == null)
        Number($count: intValue) from accumulate(
            $hypotension: SymptomEvent(patientId == $id, procedureId == $procedureId, symptom == SymptomEvent.Symptom.Hypotension) over window: time(10m), count($hypotension)
        )
        eval($count == 2)
    then
        System.out.println("Patient " + $patient.getFullname() + ": Detected hypotension. Cardiovascular complications are possible.");
        insert(new Alarm($id, $doctorId, "Detected hypotension. Cardiovascular complications are possible."));
end


// Post op
rule "Post op SAP - Crystalloids"
    when
        $patient: Patient($id: id)
        $procedure: Procedure(patientId == $id, $doctorId: medicalStaffId, postOperative != null)
        Number($averageVal: doubleValue) from accumulate(
            $sap: SAPEvent(patientId == $id) over window: time(5m), average($sap.getValue())
        )
        eval($averageVal < ($patient.getBasalSAP() - 20) && $averageVal > ($patient.getBasalSAP() - 50))
    then
        System.out.println("Patient " + $patient.getFullname() + ": Initiate IV crystalloids and alert anesthesiologist.");
        insert(new Alarm($id, $doctorId, "Initiate IV crystalloids and alert anesthesiologist."));
       
end

rule "Post op SAP - Vasopressors Infusion"
    when
        $patient: Patient($id: id, $basalSAP: basalSAP)
        $procedure: Procedure(patientId == $id, $doctorId: medicalStaffId, postOperative != null)
        Number($averageVal: doubleValue) from accumulate(
            $sap: SAPEvent(patientId == $id) over window: time(5m), average($sap.getValue())
        )
        eval($averageVal <= ($basalSAP - 50))
    then
        System.out.println("Patient " + $patient.getFullname() + ": Start vasopressors infusion.");
        insert(new Alarm($id, $doctorId, "Start vasopressors infusion."));
        insert(new VasopressorIV($id));
        
end

rule "Post op SAP - Vasopressors IV still low"
    when
        $patient: Patient($id: id, $basalSAP: basalSAP)
        $procedure: Procedure(patientId == $id, $doctorId: medicalStaffId, postOperative != null)
        $vasopressorIV: VasopressorIV(patientId == $id)
        $SAPEvent: SAPEvent(this after[15m] $vasopressorIV, patientId == $id, value <= ($basalSAP - 50))
    then
        System.out.println("Patient " + $patient.getFullname() + ": determine hemoglobin.");
        insert(new Alarm($id, $doctorId, "Determine hemoglobin."));
end

rule "Hemoglobin results"
    when 
        $patient: Patient($id: id)
        $procedure: Procedure(patientId == $id, $doctorId: medicalStaffId, postOperative != null, postOperative.getHemoglobin() < 70 && postOperative.getHemoglobin() > 0)
    then
        System.out.println("Patient " + $patient.getFullname() + ": give blood transfusion.");
        insert(new Alarm($id, $doctorId, "Give patient blood transfusion."));
end

rule "Post op SAP - Inc"
    when
        $patient: Patient($id: id)
        $procedure: Procedure(patientId == $id, $doctorId: medicalStaffId, postOperative != null)
        Number($averageVal: doubleValue) from accumulate(
            $sap: SAPEvent(patientId == $id) over window: time(5m), average($sap.getValue())
        )
        eval($averageVal >= ($patient.getBasalSAP() + 20) && $averageVal <= ($patient.getBasalSAP() + 50))
    then
        System.out.println("Patient " + $patient.getFullname() + ": бол, делиријум, мучнина и повраћање, плитко дисање.");
        insert(new Alarm($id, $doctorId, "Determine if has pain, delirium, nausea and vomiting or shallow breathing."));
        // Todo add alert
end

rule "Post op SAP - Inc 50"
    when
        $patient: Patient($id: id, $basalSAP: basalSAP)
        $procedure: Procedure(patientId == $id, $doctorId: medicalStaffId, postOperative != null)
        Number($averageVal: doubleValue) from accumulate(
            $sap: SAPEvent(patientId == $id) over window: time(5m), average($sap.getValue())
        )
        eval($averageVal >= ($basalSAP + 50))
    then
        System.out.println("Patient " + $patient.getFullname() + ": give hypertensive.");
        insert(new Alarm($id, $doctorId, "Give patient hypertensive."));
        // Todo add alert
end

rule "hypoxemia rule"
    when
        $patient: Patient($id: id)
        $procedure: Procedure(patientId == $id, $procedureId: id, postOperative != null)
        Number($count : doubleValue) from accumulate(
            $pulseOximetry: PulseOximetryEvent(patientId == $id)
            over window: time(5m), average($pulseOximetry.getValue())
        )
        eval($count < 92.0 && $count > 0.0)
    then
        System.out.println("Hypoxemia detected.");
        insert(new SymptomEvent($id, $procedureId, SymptomEvent.Symptom.Hypoxemia));
end

rule "hypoxemia alarm rule"
    when 
        $patient: Patient($id: id)
        $procedure: Procedure(patientId == $id, $doctorId: medicalStaffId, $procedureId: id, postOperative != null)
        exists(SymptomEvent(patientId == $id, procedureId == $procedureId, symptom == SymptomEvent.Symptom.Hypoxemia))
        Number($count : doubleValue) from accumulate(
            $pulseOximetry: PulseOximetryEvent(patientId == $id)
            over window: time(5m), average($pulseOximetry.getValue())
        )
        eval($count < 90.0 && $count > 0.0)
    then
        System.out.println("Patient " + $patient.getFullname() + ": Connect patient to ventilator.");
        insert(new Alarm($id, $doctorId, "Connect patient to ventilator."));
end

rule "cyanosis rule"
    when 
        $patient: Patient($id: id)
        $procedure: Procedure(patientId == $id, $procedureId: id, postOperative != null)
        Number($count : doubleValue) from accumulate(
            $pulseOximetry: PulseOximetryEvent(patientId == $id)
            over window: time(5m), average($pulseOximetry.getValue())  
        )
        eval($count < 67.0 && $count > 0.0)
    then
        System.out.println("Cyanosis detected.");
        insert(new SymptomEvent($id, $procedureId, SymptomEvent.Symptom.Cyanosis));
end

rule "Breath frequency - tachypnea"
    when
        $patient: Patient($id: id)
        $procedure: Procedure(patientId == $id, $procedureId: id, postOperative != null)
        Number($count: intValue) from accumulate(
            $e: BreathEvent(patientId == $id)
            over window: time(1m), count($e)
        )
        eval($count > 20)
    then
        System.out.println("Patient " + $patient.getFullname() + ": Tachypnea detected.");
        insert(new SymptomEvent($id, $procedureId, SymptomEvent.Symptom.Tachypnea));
end

rule "Breath frequency - bradypnea"
    when
        $patient: Patient($id: id)
        $procedure: Procedure(patientId == $id, $procedureId: id, postOperative != null)
        Number($count: intValue) from accumulate(
            $e: BreathEvent(patientId == $id)
            over window: time(1m), count($e)
        )
        eval($count < 12 && $count > 0)
    then
        System.out.println("Patient " + $patient.getFullname() + ": Bradypnea detected.");
        insert(new SymptomEvent($id, $procedureId, SymptomEvent.Symptom.Bradypnea));
end
