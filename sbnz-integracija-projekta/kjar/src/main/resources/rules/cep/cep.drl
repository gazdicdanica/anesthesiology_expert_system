package cep;

import com.ftn.sbnz.model.patient.Patient;
import com.ftn.sbnz.model.procedure.Procedure;
import com.ftn.sbnz.model.events.HeartBeatEvent;
import com.ftn.sbnz.model.events.ExstrasystoleEvent;
import com.ftn.sbnz.model.events.SAPEvent;
import com.ftn.sbnz.model.events.HypotensionEvent;
import com.ftn.sbnz.model.events.HypertensionEvent;
import com.ftn.sbnz.model.events.BradycardiaEvent;
import com.ftn.sbnz.model.events.TachycardiaEvent;
import com.ftn.sbnz.model.events.PulseOximetryEvent;
import com.ftn.sbnz.model.events.HypoxemiaEvent;
import com.ftn.sbnz.model.events.CyanosisEvent;


declare Alarm
    patientId: Long
    doctorId: Long
    message: String
end

declare VasopressorIV
    @role(event)
    @expires(16m)
    patientId: Long
end


rule "exstrasystole rule"
    when
        $patient : Patient($id: id)
        $procedure: Procedure(patientId == $id, $doctorId: medicalStaffId)
        Number($count : intValue) from accumulate(
            $exs: ExstrasystoleEvent(patientId == $id)
            over window: time(5m), count($exs)
        )
        eval($count >= 25)
    then
        //предиктор срчаних компликација, алармирај лекара
        insert(new Alarm($id, $doctorId, "Detected more than 25 exstrasystoles in 5 minutes. Cardiovascular complications are possible."));
end

rule "tachycardia rule"
    when
        $patient: Patient($id: id)
        Number($count : intValue) from accumulate(
            $heartBeat: HeartBeatEvent(patientId == $id)
            over window: time(1m), count($heartBeat)
        )
        eval($count > 80)
    then
        //tahikardija
        insert(new TachycardiaEvent($id));
end

rule "tachycardia alarm"
    when 
        $patient: Patient($id: id)
        $procedure: Procedure(patientId == $id, $doctorId: medicalStaffId)
        exists(TachycardiaEvent(patientId == $id))
    then
        insert(new Alarm($id, $doctorId, "Detected tachycardia."));
end

rule "bradycardia rule"
    when
        $patient: Patient($id: id)
        Number($count : intValue) from accumulate(
            $heartBeat: HeartBeatEvent(patientId == $id)
            over window: time(1m), count($heartBeat)
        )
        eval($count < 60)
    then
        //bradikardija
        insert(new BradycardiaEvent($id));    
end

rule "bradycardia alarm"
    when 
        $patient: Patient($id: id)
        $procedure: Procedure(patientId == $id, $doctorId: medicalStaffId)
        exists(BradycardiaEvent(patientId == $id))
    then
        insert(new Alarm($id, $doctorId, "Detected bradycardia."));
end

rule "hypertension rule"
    when
        $patient: Patient($id: id, $basalSAP: basalSAP)
        Number($count : doubleValue) from accumulate(
            $sap: SAPEvent(patientId == $id) over window: time(5m), average($sap.getValue())
        )
        eval($count > 130 || $count > ($basalSAP * 1.2))
    then
        //hipertenzija
        insert(new HypertensionEvent($id));
end

rule "hypertension alarm rule"
    when
        $patient: Patient($id: id)
        $procedure: Procedure(patientId == $id, $doctorId: medicalStaffId)
        exists(HypertensionEvent(patientId == $id))
    then
        insert(new Alarm($id, $doctorId, "Detected hypertension."));
end

rule "hypotension rule"
    when
        $patient: Patient($id: id)
        Number($averageVal : doubleValue) from accumulate(
            $sap: SAPEvent(patientId == $id)
            over window: time(5m), average($sap.getValue())
        )
        eval($averageVal < 80 || ($averageVal < ($patient.getBasalSAP() * 0.8)))
    then
        insert(new HypotensionEvent($id));
end

rule "hypotension alarm rule"
    when
        $patient: Patient($id: id)
        $procedure: Procedure(patientId == $id, $doctorId: medicalStaffId)
        Number($count: intValue) from accumulate(
            $hypotension: HypotensionEvent(patientId == $id) over window: time(10m), count($hypotension)
        )
        eval($count == 2)
    then
        insert(new Alarm($id, $doctorId, "Detected hypotension. Cardiovascular complications are possible."));
end


// Post op
rule "Post op SAP - Crystalloids"
    when
        $patient: Patient($id: id)
        $procedure: Procedure(patientId == $id, $doctorId: medicalStaffId)
        $averageVal: Number(intValue <= ($patient.getBasalSAP() - 20) && intValue >= ($patient.getBasalSAP() - 50))
        from accumulate(
            $sap: SAPEvent(patientId == $id) over window: time(5m),
            average($sap.getValue())
        )
    then
        System.out.println("Patient " + $patient.getFullname() + ": Initiate IV crystalloids and alert anesthesiologist.");
        insert(new Alarm($id, $doctorId, "Initiate IV crystalloids and alert anesthesiologist."));
       
end

rule "Post op SAP - Vasopressors Infusion"
    when
        $patient: Patient($id: id, $basalSAP: basalSAP)
        $procedure: Procedure(patientId == $id, $doctorId: medicalStaffId)
        $averageVal: Number(intValue <= ($basalSAP - 50))
            from accumulate(
                $sap: SAPEvent(patientId == $id) over window: time(5m),
                average($sap.getValue())
            )
    then
        System.out.println("Patient " + $patient.getFullname() + ": Start vasopressors infusion.");
        insert(new Alarm($id, $doctorId, "Start vasopressors infusion."));
        insert(new VasopressorIV($id));
        
end

rule "Post op SAP - Vasopressors IV still low"
    when
        $patient: Patient($id: id, $basalSAP: basalSAP)
        $procedure: Procedure(patientId == $id, $doctorId: medicalStaffId)
        $vasopressorIV: VasopressorIV(patientId == $id)
        $SAPEvent: SAPEvent(this after[15m] $vasopressorIV, patientId == $id, value <= ($basalSAP - 50))
    then
        System.out.println("Patient " + $patient.getFullname() + ": determine hemoglobin.");
        insert(new Alarm($id, $doctorId, "Determine hemoglobin."));
        
end

rule "Post op SAP - Inc"
    when
        $patient: Patient($id: id)
        $averageVal: Number(intValue >= ($patient.getBasalSAP() + 20) && intValue <= ($patient.getBasalSAP() + 50))
        from accumulate(
            $sap: SAPEvent(patientId == $id) over window: time(5m),
            average($sap.getValue())
        )
    then
        System.out.println("Patient " + $patient.getFullname() + ": бол, делиријум, мучнина и повраћање, плитко дисање.");
        // Todo add alert
end

rule "Post op SAP - Inc 50"
    when
        $patient: Patient($id: id, $basalSAP: basalSAP)
        $averageVal: Number(intValue >= ($basalSAP + 50))
            from accumulate(
                $sap: SAPEvent(patientId == $id) over window: time(5m),
                average($sap.getValue())
            )
    then
        System.out.println("Patient " + $patient.getFullname() + ": дати хипертензив.");
        // Todo add alert
end

rule "oximetry rule"
    when
        $patient: Patient($id: id)
        Number($count : doubleValue) from accumulate(
            $pulseOximetry: PulseOximetryEvent(patientId == $id)
            over window: time(5m), average($pulseOximetry.getValue())
        )
        eval($count < 92.0)
    then
        System.out.println("Hypoxemia detected.");
        insert(new HypoxemiaEvent($id));
end

rule "cyanosis rule"
    when 
        $patient: Patient($id: id)
        Number($count : doubleValue) from accumulate(
            $pulseOximetry: PulseOximetryEvent(patientId == $id)
            over window: time(5m), average($pulseOximetry.getValue())  
        )
        eval($count < 67.0)
    then
        System.out.println("Cyanosis detected.");
        insert(new CyanosisEvent($id));
end
